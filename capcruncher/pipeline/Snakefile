import os
import sys
import pathlib
import shutil

import pandas as pd
import pyranges as pr
import snakemake.utils
import capcruncher.pipeline.utils

snakemake.utils.min_version('7.19.1')


configfile: "config.yml"


# Pipeline set-up
capcruncher.pipeline.utils.format_config_dict(config)

## Get fastq files
fastq_samples = capcruncher.pipeline.utils.GenericFastqSamples.from_files(
    list(pathlib.Path(".").glob("*.fastq*"))
)

## Convert FASTQ files to design matrix
if os.path.exists(config["analysis"].get("design", None)):
    DESIGN = pd.read_table(
        config["analysis"]["design"], sep=r"\s+|,|\t", engine="python"
    )
else:
    DESIGN = fastq_samples.experimental_design

## Read viewpoints
VIEWPOINTS = config["analysis"]["viewpoints"]
VIEWPOINT_NAMES = pd.read_table(
    VIEWPOINTS, names=["chrom", "start", "end", "name"], header=None
)["name"].to_list()


N_SAMPLES = DESIGN["sample"].nunique()
ANALYSIS_METHOD = config["analysis"].get("method", "capture")
HIGH_NUMBER_OF_VIEWPOINTS = capcruncher.pipeline.utils.has_high_viewpoint_number(
    VIEWPOINTS, config
)

## Optional
PERFORM_PLOTTING = capcruncher.pipeline.utils.can_perform_plotting(config)
FASTQ_DEDUPLICATE = config["deduplication"].get("pre-dedup", False)
COMPRESS_FASTQ = config["pipeline"].get("compression", 0) != 0

## Pipeline variables
SAMPLE_NAMES = DESIGN["sample"]
FASTQ_FILE_NAMES = [
    *fastq_samples.sample_details["fq1"].apply(str).to_list(),
    *fastq_samples.sample_details["fq2"].apply(str).to_list(),
]


include: "rules/digest.smk"
include: "rules/fastq.smk"
include: "rules/qc_fastq.smk"
include: "rules/align.smk"
include: "rules/annotate.smk"
include: "rules/filter.smk"
include: "rules/pileup.smk"
include: "rules/compare.smk"
include: "rules/statistics.smk"
include: "rules/visualise.smk"


rule all:
    input:
        qc=rules.multiqc_raw.output.report,
        bigwigs_per_viewpoint=expand(
            "capcruncher_pileup/05_bigwig/{sample}/{norm}/{sample}.{norm}.{viewpoint}.bw",
            sample=SAMPLE_NAMES,
            norm=["raw", "norm"],
            viewpoint=VIEWPOINT_NAMES,
        ),
        bigwigs_compared=expand(
            "capcruncher_compare/03_bigwig/{comparison}/{comparison}.{method}-subtraction.{viewpoint}.bw",
            comparison=[
            f"{a}-{b}"
                for a, b in itertools.permutations(DESIGN["condition"].unique(), 2)
            ],
            method=get_summary_methods(),
            viewpoint=VIEWPOINT_NAMES,
        ),
        bigwigs_summarised=expand(
            "capcruncher_compare/03_bigwig/{group}/{group}.{method}-summary.{viewpoint}.bw",
            group=DESIGN["condition"].unique(),
            method=get_summary_methods(),
            viewpoint=VIEWPOINT_NAMES,
        ),
        hub=rules.create_ucsc_hub.output[0],


onerror:
    log_out = "capcruncher_error.log"
    shutil.copyfile(log, log_out)
    print(
        f"An error occurred. Please check the log file {log_out} for more information."
    )
